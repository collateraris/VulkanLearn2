#version 460

#extension GL_EXT_nonuniform_qualifier:enable
#extension GL_EXT_shader_explicit_arithmetic_types: enable
#extension GL_EXT_shader_explicit_arithmetic_types_int8: enable
#extension GL_EXT_shader_explicit_arithmetic_types_float16: enable
#extension GL_NV_mesh_shader: require

#extension GL_GOOGLE_include_directive: require

#extension GL_ARB_shader_draw_parameters: require

#include "gbuffer.h"

layout(local_size_x = 32, local_size_y = 1, local_size_z = 1) in;
layout(triangles, max_vertices = 64, max_primitives = 124) out;

layout (set = 0, binding = 0) readonly buffer _vertices
{
	SVertex vertices[];
} Vertex[];

layout (set = 0, binding = 1) readonly buffer _meshlets
{
	SMeshlet meshlets[];
} Meshlet[];

layout(set = 0, binding = 2) readonly buffer _meshletData
{
	uint meshletData[];
} MeshletData[];

layout(set = 1, binding = 0) uniform _GlobalCamera { SGlobalCamera cameraData; };

layout(set = 1, binding = 1) readonly buffer ObjectBuffer{

	SObjectData objects[];
} objectBuffer;

in taskNV block
{
	uint meshletIndices[32];
};

// Custom vertex output block
layout (location = 0) out PerVertexData
{  
  flat uint drawID;
} v_out[];   // [max_vertices]

void main()
{
	uint mi = meshletIndices[gl_WorkGroupID.x];
	uint ti = gl_LocalInvocationID.x;

	mat4 modelMatrix = objectBuffer.objects[gl_DrawID].model;
	mat3 normalMatrix = transpose(inverse(mat3(modelMatrix)));
	uint meshletIndex = objectBuffer.objects[gl_DrawID].meshIndex;
	SMeshlet currMeshlet = Meshlet[meshletIndex].meshlets[mi];

	uint meshIndex = meshletIndex;

	uint vertexCount = uint(currMeshlet.vertexCount);
	uint triangleCount = uint(currMeshlet.triangleCount);
	uint indexCount = triangleCount * 3;

	uint dataOffset = currMeshlet.dataOffset;
	uint vertexOffset = dataOffset;
	uint indexOffset = dataOffset + vertexCount;

	// TODO: if we have meshlets with 62 or 63 vertices then we pay a small penalty for branch divergence here - we can instead redundantly xform the last vertex
	for (uint i = ti; i < vertexCount; i += 32)
	{
		uint vi = MeshletData[meshletIndex].meshletData[vertexOffset + i];
		SVertex vert = Vertex[meshletIndex].vertices[vi];

		vec3 position = vert.positionXYZ_normalX.xyz;
		position = vec3(modelMatrix * vec4(position, 1.0f));

		gl_MeshVerticesNV[i].gl_Position = cameraData.viewProj * vec4(position, 1.0f);
        v_out[i].drawID = uint(gl_DrawID);
	}

    for (uint i = ti; i < triangleCount; i += 32)
	{
		uint index0 = i * 3 + 0;
		uint index1 = i * 3 + 1;
		uint index2 = i * 3 + 2;

		uint vertex0Id = uint(MeshletData[meshletIndex].meshletData[indexOffset + index0]);
		gl_PrimitiveIndicesNV[index0] = vertex0Id;
		gl_PrimitiveIndicesNV[index1] = uint(MeshletData[meshletIndex].meshletData[indexOffset + index1]);
		gl_PrimitiveIndicesNV[index2] = uint(MeshletData[meshletIndex].meshletData[indexOffset + index2]);

		gl_MeshPrimitivesNV[i].gl_PrimitiveID = int(vertex0Id);
	}

	if (ti == 0)
		gl_PrimitiveCountNV = uint(currMeshlet.triangleCount);
}